\subsection{划分树}
\begin{verbatim}
{
划分树支持静态询问,询问方式为区间[le,ri]中的第k大/小的数
}
Const  maxn=100000;

var
  n,m:longint;
  a:array[0..maxn] of longint;
  toLeft,value:array[0..trunc(ln(maxn)/ln(2))+1,1..maxn] of longint;

procedure qsort(l,r:longint);
var
  i,j,m:longint;
begin
  i:=l; j:=r; m:=a[(i+j) shr 1];
  repeat
    while a[i]<m do inc(i);
    while a[j]>m do dec(j);
    if i<=j then
    begin
      a[0]:=a[i];
      a[i]:=a[j];
      a[j]:=a[0];
      inc(i); dec(j);
    end;
  until i>j;

  if l<j then qsort(l,j);
  if i<r then qsort(i,r);
end;

procedure init;
var
  i:longint;
begin
  readln(n,m);
  for i:=1 to n do read(a[i]);
        for i:=1 to n do value[0,i]:=a[i];
  qsort(1,n);
end;

procedure BuildTree(x,y,deep:longint);
var
  i,mid,le,ri,same,lsame:longint;
begin
  if x=y then exit;
  mid:=(x+y) shr 1; lsame:=mid-x+1;
  for i:=x to y do
    if value[deep][i]<a[mid] then dec(lsame);
  le:=x; ri:=mid+1; same:=0;
  for i:=x to y do
  begin
    if i=x then toLeft[deep,i]:=0 else toLeft[deep,i]:=toLeft[deep,i-1];
    if value[deep][i]<a[mid] then
    begin
      inc(toLeft[deep,i]);
      value[deep+1,le]:=value[deep,i];
      inc(le);
    end else
    if value[deep][i]>a[mid] then
    begin
      value[deep+1,ri]:=value[deep][i];
      inc(ri);
    end else
    begin
      if same<lsame then
      begin
        inc(toLeft[deep,i]);
        value[deep+1,le]:=value[deep,i];
        inc(le); inc(same);
      end else
      begin
        value[deep+1,ri]:=value[deep,i];
        inc(ri);
      end;
    end;
  end;

  BuildTree(x,mid,deep+1);
  BuildTree(mid+1,y,deep+1);
end;

function ask(le,ri,x,y,deep,kth:longint):longint;
var
  s1,s2,mid:longint;
begin
  if x=y then exit(value[deep,x]);
  mid:=(x+y) shr 1;
  if x=le then
  begin
    s1:=0;
    s2:=toLeft[deep,ri];
  end else
  begin
    s1:=toLeft[deep,le-1];
    s2:=toLeft[deep,ri];
  end;
  if s2-s1>=kth then
  begin
    le:=x+s1; ri:=le+s2-s1-1;
    exit(ask(le,ri,x,mid,deep+1,kth));
  end else
  begin
    kth:=kth-(s2-s1);  s2:=ri-le+1-(s2-s1);
    le:=mid+le-x+1-s1; ri:=le+s2-1;
    exit(ask(le,ri,mid+1,y,deep+1,kth));
  end;
end;

procedure main;
var
  i,kth,le,ri:longint;
begin
  BuildTree(1,n,0);
  for i:=1 to m do
  begin
    readln(le,ri,kth);
    writeln(ask(le,ri,1,n,0,kth));
  end;
end;

begin
  init;
  main;
end.
\end{verbatim}
